/**
 * 工程类库共用相关配置
 */

//限制在跟目录导入
if (project.name != project.rootProject.name) {
    return
}

println("Custome Configure configModule.gradle")


subprojects {
    if (it.name == 'app'
            || it.name.startsWith('app-')
            || it.name.startsWith('module-')) {

        if (it.name.startsWith('module-java')) {
            return
        }
        if (useComponent) {
            //使用组件
            if (it.name == 'app-loading') {
                apply plugin: 'com.android.library'
            } else {
                if (it.name == 'app') {
                    ext.mainApp = true
                }
                apply from: rootProject.file('gradle/cc-settings-3.gradle')
            }
        } else {
            //不使用组件
            if (it.name == 'app') {
                apply plugin: 'com.android.application'
            } else {
                apply plugin: 'com.android.library'
            }
        }
        //app和module都生成R2文件，因为module要使用R2，切换为application的时候不用替换R2为R
        apply plugin: 'com.jakewharton.butterknife'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply plugin: 'kotlin-android-extensions'
        //保存页面字段
        //apply plugin: 'save.state'

        apply plugin: 'bga-appinit-plugin'
//        appInit {
//            abortOnNotExist true
//        }

        if (it.ext.has("mainApp") && it.ext.mainApp == true) {
            apply from: rootProject.file('gradle/configApp.gradle')
        }

        if (!useNew && !useOld) {
            apply from: rootProject.file('gradle/novoda-bintray.gradle')
        }

        android.compileOptions {
            targetCompatibility Versions.compileJdkVersion
            sourceCompatibility Versions.compileJdkVersion
        }

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
                arg("loginOuterKey", "needLogin")
                arg("loginInnerKey", "needLogin")
                arg("serializer", "gson")
            }
        }
        boolean isApplication = it.pluginManager.hasPlugin("com.android.application")
        println("subproject" + "(" + project.name + "):" + "[isApplication:" + isApplication + "]")

        //除了app的其他module
        if (it.name != 'app') {
            android {
                compileSdkVersion build_versions.compileSdkVersion
                buildToolsVersion build_versions.buildToolsVersion

                defaultConfig {
                    if (isApplication) {
                        applicationId Versions.applicationId
                    }
                    minSdkVersion Versions.minSdkVersion
                    targetSdkVersion Versions.targetSdkVersion

                    versionCode Versions.versionCode
                    versionName Versions.versionName
                }

                buildTypes {
                    debug {}
                    beta {}
                    release {}
                }
                buildTypes.all { type ->
                    type.matchingFallbacks = ['debug']
                    type.proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro']
                }
                lintOptions {
                    abortOnError false
                }
                packagingOptions {
                    exclude 'META-INF/proguard/androidx-annotations.pro'
                    exclude 'META-INF/androidx.core_core.version'
                    exclude 'META-INF/androidx.versionedparcelable_versionedparcelable.version'
                }
                dataBinding { enabled = true }
            }
        }
        //app模块公用的配置参数
        android {
            kotlinOptions {
                jvmTarget = "1.8"
            }

            defaultConfig {
                ndk { abiFilters "armeabi-v7a" }
//                ndk { abiFilters "armeabi-v7a", "x86" }
                resConfigs "zh", "xxhdpi"

                manifestPlaceholders = [qq_id: qqId]

                if (!useOld) {
                    viewBinding { enabled = true }
                }
            }

        }

        String tempName = it.name

        dependencies {
            implementation fileTree(include: ['*.jar'], dir: 'libs')

            implementation project(':application')
            implementation project(':component-service')

            if (tempName != 'app-loading') {
                api project(':app-loading')
            }

            if (useMaven) {
            } else {
                //dagger
                kapt Deps.dagger_compiler
                kapt Deps.dagger_android_compiler
                //lifecycle
                kapt Deps.lifecycle_compiler
                //db
                kapt Deps.room_compiler
                //butterknife
                kapt Deps.butterknife_compiler
                //跳转路由
                kapt Deps.arouter_compiler
                //测试
                kapt project(':lib-component-compiler')
                //本地路由映射表
                kapt project(':lib-jump-compiler')
                //本地mvp映射表
                implementation project(':lib-mvp-annotation')
                //                kapt project(':lib-mvp-compiler')
            }

        }
    }
}
