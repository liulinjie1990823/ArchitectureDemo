// load properties
Properties localProperties = new Properties()
Properties properties = new Properties()

File localPropertiesFile = project.parent.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newDataInputStream())
} else {
    println("local.properties not found")
}

File projectPropertiesFile = project.file("project.properties")
if (projectPropertiesFile.exists()) {
    properties.load(projectPropertiesFile.newDataInputStream())
} else {
    println("project.properties not found")
}

// read properties
def projectUserOrg = properties.getProperty("project.userOrg")
def projectRepo = properties.getProperty("project.repo")

def projectGroupId = properties.getProperty("project.groupId")
def projectArtifactId = properties.getProperty("project.artifactId")
def projectVersionName = properties.getProperty("project.versionName")

def projectName = properties.getProperty("project.name")
def projectPackaging = properties.getProperty("project.packaging")
def projectDescription = properties.getProperty("project.description")
def webSiteUrl = properties.getProperty("project.webSiteUrl")
def issueUrl = properties.getProperty("project.issueTrackerUrl")
def vscUrl = properties.getProperty("project.vscUrl")
def projectLicense = properties.getProperty("project.license")

def developerId = properties.getProperty("developer.id")
def developerName = properties.getProperty("developer.name")
def developerEmail = properties.getProperty("developer.email")

def bintrayUser = localProperties.getProperty("bintray.user")
def bintrayApikey = localProperties.getProperty("bintray.apikey")

def javadocName = properties.getProperty("javadoc.name")


//gradle1.3版本新增maven-publish
//https://docs.gradle.org/current/userguide/publishing_maven.html
//用于发布本地或远程自定义仓库，MavenLocal

//增加publishing组
//设置了自定义的repositories（仓库可以是远程也可以是本地的）
//generateMetadataFileForMavenJava1Publication
//generatePomFileForMavenJava1Publication
//publish
//publishAllPublicationsToLocalRepoRepository
//publishMavenJava1PublicationToLocalRepoRepository
//publishMavenJava1PublicationToMavenLocal
//publishToMavenLocal

//没有设置repositories
//generateMetadataFileForMavenJava1Publication
//generatePomFileForMavenJava1Publication
//publish
//publishMavenJava1PublicationToMavenLocal
//publishToMavenLocal

//publications里可以设置多个组件
//repositories可以不设置，默认提供发布到MavenLocal
apply plugin: 'maven-publish'

//定义文档task
if (project.getPlugins().hasPlugin('com.android.application') ||
        project.getPlugins().hasPlugin('com.android.library')) {
    //文档
    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.source
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
    //文档
    task javadocJar(type: Jar, dependsOn: androidJavadocs) {
        from androidJavadocs.destinationDir
        classifier = 'javadoc'
    }
    //源码
    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.source
        classifier = 'sources'
    }
} else {
    //文档
    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }
    //源码
    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
}


publishing {
    //设置发布内容
    publications {
        mavenJava1(MavenPublication) {

            //Standard metadata
            groupId = projectGroupId
            artifactId = projectArtifactId
            version = projectVersionName

            //Custom artifacts
            artifact javadocJar
            artifact sourcesJar

            //设置pom文件
            pom {
                name = projectName
                packaging = projectPackaging
                description = projectDescription
                url = webSiteUrl

                scm {
                    connection = vscUrl
                    developerConnection = vscUrl
                    url = webSiteUrl
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
            }
        }
    }

    //设置发布仓库
    repositories {
        maven {
            //可以使本地路径，也可以是远程仓库
            url = "../repo"
            //指定要上传的maven私服仓库
//            url = "http://jenkins.maxrocky.com:8080/maven/content/repositories/thirdparty/"
            //远程的仓库名称，一般url就指定了仓库名称
            name = "localRepo"

            //认证用户和密码
//            credentials {
//                username 'admin'
//                password 'maxrocky5721'
//            }
        }
    }
}

