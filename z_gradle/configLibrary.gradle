/**
 * 工程类库共用相关配置
 */

//限制在跟目录导入
if (project.name != project.rootProject.name) {
    return
}

println("Custome Configure (configLibrary.gradle)")


subprojects {
    //配置类库公用的配置
    if (it.name.startsWith('application')
            || it.name.startsWith('component-service')
            || it.name.startsWith('lib-')
            || it.name.startsWith('lib_')
            || it.name.startsWith('plugin-')) {
        if (it.name.startsWith('plugin-code-inject')
                || it.name.startsWith('plugin-upload')) {
            apply plugin: 'groovy'

        } else if (it.name.startsWith('lib-component-annotation')
                || it.name.startsWith('lib-component-compiler')
                || it.name.startsWith('lib-jump-annotation')
                || it.name.startsWith('lib-jump-compiler')
                || it.name.startsWith('lib-base-compiler')
                || it.name.startsWith('lib-mvp-compiler')
                || it.name.startsWith('lib-mvp-annotation')) {
            apply plugin: 'java-library'
            sourceCompatibility = "1.8"
            targetCompatibility = "1.8"

        } else {
            apply plugin: 'com.android.library'

            android.compileOptions {
                targetCompatibility Versions.compileJdkVersion
                sourceCompatibility Versions.compileJdkVersion
            }

            android {
                compileSdkVersion Versions.compileSdkVersion
                buildToolsVersion Versions.buildToolsVersion

                defaultConfig {
                    minSdkVersion Versions.minSdkVersion
                    targetSdkVersion Versions.targetSdkVersion

//                    ndk { abiFilters "armeabi-v7a" }
                    ndk { abiFilters "armeabi-v7a", "x86" }
                }

                buildTypes {
                    debug {}
                    beta {}
                    release {}
                }
                buildTypes.all { type ->
                    type.matchingFallbacks = ['debug']
                    type.minifyEnabled = false
                    type.proguardFiles = [getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro']
                }

                lintOptions {
                    abortOnError false
                }
                packagingOptions {
                    exclude 'META-INF/proguard/androidx-annotations.pro'
                    exclude 'META-INF/androidx.core_core.version'
                    exclude 'META-INF/androidx.versionedparcelable_versionedparcelable.version'

                    pickFirst '**/libjsc.so'
                    pickFirst '**/libc++_shared.so'
                }
            }
            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])
            }
        }
        //android-plugin升级后统一使用novoda-bintray来打包，之前的和插件有冲突
        if ( !useOld) {
            apply from: rootProject.file('z_gradle/novoda-bintray.gradle')
        }
//        if (useOld) {
//            //打包本地
//            apply from: rootProject.file('gradle/uploadArchives.gradle')
//            //上传bintray
//            apply from: rootProject.file('gradle/bintrayUpload.gradle')
//        }
    }
}
